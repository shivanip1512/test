prototype LONG kernel32.GetEnvironmentVariable(BYVAL STRING, BYREF STRING, BYREF LONG);
// Included header files
#include "ifx.h"
#include "ISRTWindows.h"
#include "featureevents.rul"
#include "util.rul"

//for windows api message broadcasting
#define WM_SETTINGCHANGE 		0x001A

//constants
#define SYS32			"C:\\WINDOWS\\System32\\"
#define SERVICE_WARNING	"Setup detected a Yukon service still running.\nPlease make sure that all Yukon services are stopped before continuing."   

//---------------------------------------------------------------------------
// FirstUIBefore event is sent when installation is run for the first
// time on given machine. After this function returns, FeatureTransferData 
// is called to perform file transfer.
//---------------------------------------------------------------------------
function OnFirstUIBefore()
number nResult, nSize;
string sDir, sDirLow, sDestWarn, sDateString, formattedDateString;

begin
	//set the string shown on the installer windows' title bars
	IFX_SETUP_TITLE = "Yukon Application Suite - Installation Wizard";
		
	Dialog_Welcome:
	Disable(BACKBUTTON);
	TARGETDIR = "C:\\Yukon";
	SdWelcome("Yukon Application Suite", 
			  "Welcome to the installer for the Yukon Application suite."
			  + "\nThis program will install Yukon " + IFX_PRODUCT_VERSION
			  + " on your computer.\nTo continue, click Next.");
    RemoveCalcHist();
	Dialog_AskDestinationPath:
	Enable(BACKBUTTON);
	sDir = TARGETDIR;
	nSize = 0;
	sDestWarn = "Please select a setup type and destination. If the "
	+ "destination is changed, a restart will be required after "
	+ "installation.";
	nResult = SdSetupType2("", sDestWarn, sDir, 0);
	if (nResult = BACK) goto Dialog_Welcome;
	//if (nResult = COMPLETE) - just keep going, all features selected by default
	if (nResult = CUSTOM) then
		nResult = SdFeatureTree("", "", TARGETDIR, "", 2);
		if(nResult = BACK) goto Dialog_AskDestinationPath;
	endif;
	
	StrToLower(sDirLow, sDir);
	StrRemoveLastSlash(sDirLow);
	
	//if the install destination isn't C:\Yukon, mandate a restart after
	//install, so the services can pick up YUKON_BASE.
	if(StrCompare(sDirLow, "c:\\yukon") != 0) then
		BATCH_INSTALL = TRUE;
	endif;
	
	//if the last five chars of the path aren't "yukon", 
	//add it to the end of the path
	if(StrFindEx(sDirLow, "yukon", StrLengthChars(sDirLow)-5) < 0) then
		sDir = sDir ^ "Yukon";
	endif;
	
    GetSystemInfo(DATE, nResult, sDateString);
    // Format date pattern in YYYYMMDD
    formattedDateString = FormatDatePattern(sDateString);
    if(!CheckForBackUpFiles()) then
    BackupLoggingConfigFiles(formattedDateString);
    endif;
    RenameInstalledDateJavaServicesAndClientLog(formattedDateString);

	//make sure we have enough space for the selected features
	TARGETDIR = sDir;
	FeatureCompareSizeRequired(MEDIA, TARGETDIR, nSize);
	if (nSize != 0) then
		MessageBox(szSdStr_NotEnoughSpace, WARNING);
		goto Dialog_AskDestinationPath;
	endif;
    
    Dialog_StartCopy:
    nResult = SdStartCopy2("Ready to Install Yukon", "");
    if(nResult = BACK) then
    	goto Dialog_AskDestinationPath:
    endif;
    
	//Activate status window and set completion bar to 0% before
	//file moving starts
    SetStatusWindow(0, "");
    Enable(STATUSEX);
    StatusUpdate(ON, 100);   
	return 0;
end;

//---------------------------------------------------------------------------
// The OnFirstUIAfter event called by OnShowUI after the file transfer
// of the setup when the setup is running in first install mode.
//---------------------------------------------------------------------------
function OnFirstUIAfter()
string sProg, sCMD, sName, sKey, sEnv, sPath, sFileName, sResult, sFilePath;
number nResult;
BOOL opt, bOptYes, bOptNo;
pointer pEnv;
begin
	//IMPORTANT NOTE: using LaunchApplication with the SW_HIDDEN flag will
 	//cause ALL APPLICATIONS LAUNCHED HENCEFORTH to be hidden! Even if they
 	//are launched through LaunchApp or LaunchAppAndWait.  You must call
 	//LaunchApplication with the SW_NORMAL flag to un-hide launched apps.
	LaunchApplication("", "", "", SW_NORMAL, 0, LAAW_OPTION_WAIT);
	
	//set the YUKON_BASE environment variable to the TARGETDIR
	//where we just installed Yukon
	sName = "YUKON_BASE";
	sKey = "\\SYSTEM\\CurrentControlSet\\Control\\Session Manager\\Environment";
	RegDBSetDefaultRoot(HKEY_LOCAL_MACHINE);
	nResult = RegDBSetKeyValueEx(sKey, sName, REGDB_STRING, TARGETDIR, -1);
	if(nResult != 0) then
		MessageBox("Unable to set YUKON_BASE environment variable.", WARNING);
	endif;
	
	//broadcast notification of environment variable change so it is visible
	//to other programs
	sEnv = "Environment";
	pEnv = &sEnv;
	SendMessage(HWND_BROADCAST, WM_SETTINGCHANGE, 0, pEnv);
	
    AllowWriteAccessForUsers(TARGETDIR ^ "Server\\Config\\master.cfg");
    AllowWriteAccessForUsers(TARGETDIR ^ "Server\\Config\\remoteLogging.xml");
    AllowWriteAccessForUsers(TARGETDIR ^ "Server\\Config\\yukonLogging.xml");
    
    Dialog_MasterConfig:
    Disable(BACKBUTTON);
    Disable(CANCELBUTTON); //You just installed.  There's no going back.
    bOptYes = TRUE;
    AskOptions(EXCLUSIVE,
    			"Would you like to edit the master.cfg at this time?",
    			"Yes", bOptYes,
    			"No", bOptNo);
    if(bOptYes) then
    	sProg = "notepad.exe";
        sCMD = TARGETDIR ^ "\\Server\\Config\\master.cfg";
        LongPathToQuote(sCMD, TRUE);
        LaunchAppAndWait(sProg, sCMD, LAAW_OPTION_WAIT);
    endif;
    Enable(BACKBUTTON);

    Dialog_DBToolsFrame:
    bOptYes = TRUE;
    nResult = AskOptions(EXCLUSIVE,
				"This software requires a Yukon database of version "
    			+ IFX_PRODUCT_VERSION + ".  " 
    			+ "Would you like to open the DB Tools Frame to "
    			+ "update an existing database?",
    			"Yes", bOptYes,
    			"No", bOptNo);
    if(nResult = BACK) then
    	goto Dialog_MasterConfig;
    endif;
    if(bOptYes) then
    	sProg = TARGETDIR ^ "\\Client\\bin\\DBToolsFrame.bat";
		LaunchAppAndWait(sProg, "", LAAW_OPTION_WAIT);
	endif;

    Dialog_StartServices:
	//only give the option to start services if services were installed
	//and only if a restart is not necessary
	if (FeatureIsItemSelected(MEDIA, "Yukon Server") && (!BATCH_INSTALL)) then
		bOptYes = TRUE;
		nResult = AskOptions(EXCLUSIVE,
					"Would you like to start the Yukon Services?",
					"Yes", bOptYes,
					"No", bOptNo);
		if(nResult = BACK) then
			goto Dialog_DBToolsFrame;
		endif;
		if(bOptYes) then
			StartYukonServices(TRUE);
		endif;
	endif;
    
    Dialog_InstallComplete:
    Disable(BACKBUTTON);
    if (BATCH_INSTALL) then
    	SdFinishReboot("Installation Complete", 
    				   "The installer has successfully installed the Yukon Application Suite. "
    				   +"Before you can use the program, you must restart your computer.", 
    				   SYS_BOOTMACHINE, "", 0);
    else
    	SdFinish("Installation Complete",
    		 		"The installer has successfully installed the Yukon Application Suite.",
    		 		"Click Finish to exit setup.",
    		 		"","",opt,opt);
    endif;
    CopyFile(TARGETDIR + "\\Server\\web\\conf\\server.xml", TARGETDIR + "\\Server\\Config\\Backup\\server_" + CleanupVersionNum(IFX_PRODUCT_VERSION) + ".xml");
	CopyFile(TARGETDIR + "\\Server\\web\\conf\\web.xml", TARGETDIR + "\\Server\\Config\\Backup\\web_" + CleanupVersionNum(IFX_PRODUCT_VERSION) + ".xml");
end;

//--------------------------------------------------------------------------
// The OnFileLocked event is called when a file that is in use needs to be 
// installed or uninstalled (unless the files are in a file group which is 
// marked as as potentially locked or shared in this case the file will be 
// installed or uninstalled after reboot.
//
// sFile will contain the full path of the file that is locked when the
// event is called.
//---------------------------------------------------------------------------
function OnFileLocked(sFile)
begin
	//Let the user decide what to do (cancel, retry, modify after reboot)	
	return SdExceptions(LOCKED, sFile);
end;

//---------------------------------------------------------------------------
// OnSetUpdateMode is called directly by the framework to set the UPDATEMODE
// InstallShield system variable appropriately to control which UI events
// are called by OnShowUI.
// Note: This event is called for all setups.
//---------------------------------------------------------------------------
function OnSetUpdateMode()
number	nIgnore, nMediaFlags, nInstalledVersion, nUpdateVersion, nResult;
string	szVersion, szIgnore, szMsg;
begin
	UPDATEMODE = FALSE; // Non-update mode by default.
	//Don't set UPDATEMODE when running from Add/Remove
	//or if REMOVEONLY is specified
	if (ADDREMOVE) then
		return ISERR_SUCCESS;
	endif;
	if (REMOVEONLY) then
		return ISERR_SUCCESS;
	endif;
	// Get the media flags.
	MediaGetData( MEDIA, MEDIA_FIELD_MEDIA_FLAGS, nMediaFlags, szIgnore );
	if (!(nMediaFlags & MEDIA_FLAG_UPDATEMODE_SUPPORTED)) then
		return ISERR_SUCCESS; // Update mode not supported by the setup.
	endif;
	// Verify that the installed version is valid.
	if (!StrLengthChars(IFX_INSTALLED_VERSION) && MAINTENANCE) then
		// If this error occurs, IFX_INSTALLED_VERSION needs to be set manually.
		szMsg = SdLoadString( IDS_IFX_ERROR_UPDATE_NO_INSTALLED_VERSION );
		MessageBox(szMsg, SEVERE);
		abort;
	endif;
	// Verify that the product version is valid.
	if(!StrLengthChars(IFX_PRODUCT_VERSION)) then
		// If this error occures, IFX_PRODUCT_VERSION was not initialized correctly.
		szMsg = SdLoadString(IDS_IFX_ERROR_UPDATE_NO_PRODUCT_VERSION);
		MessageBox(szMsg, SEVERE);
		abort;
	endif;
	// Do the version comparison.
	nResult = VerProductCompareVersions();
	// Make sure that valid data was returned by VerProductCompareVersions
	if(nResult < ISERR_SUCCESS) then
		szMsg = SdLoadString(IDS_IFX_ERROR_UPDATE_VERSION_COMPARE_FAILURE);
		MessageBox(szMsg, SEVERE);
		abort;
	endif;
	// Set update mode if this is a differential media or the product is already installed and the versions do not match.
	UPDATEMODE = (nMediaFlags & MEDIA_FLAG_FORMAT_DIFFERENTIAL || (MAINTENANCE && (nResult = VERSION_COMPARE_RESULT_NEWER)));
end;

//-----------------------------------------------------------------------------
// OnUpdateUIBefore event is sent when end user runs a newer verison of the
// installation that has already been installed on the machine.
//-----------------------------------------------------------------------------
function OnUpdateUIBefore()
number nResult ;
BOOL bOptYes, bOptNo, bOpt;
string sMasterConfig, sOldVersion, sMsg1, sMsg2, sCMD, sArgs, sDir, szComputerName, shortcutDir;
begin
	//set the string shown on the installer windows' title bars
	IFX_SETUP_TITLE = "Yukon Application Suite - Installation Wizard";
	
	//prettyfy the version number
	sOldVersion = CleanupInstalledVersionNum();
	
    //Check to see if upgrade-type install is supported.  This
    //requires the currently installed version to be 5.1.3 or later.
    //For earlier versions, you must uninstall the old and then
    //install the new.
	Dialog_VersionCheck:
	Disable(BACKBUTTON);
	if(!IsUpgradeSupported()) then
		sMsg1 = "An older version of the Yukon Application Suite is currently installed. "
				+ "This version does not support the Update install type.";
		sMsg2 = "To complete the update, please uninstall the old version" 
				+ " and run this installer again.";
		SdFinish("Update Install Type Not Supported", sMsg1, sMsg2, "", "", bOpt, bOpt);
		abort; //do not continue until they've uninstalled the old version
	endif;
	
	Dialog_Welcome:
	Disable(BACKBUTTON);
	nResult = SdWelcome("Yukon Application Suite",
					    "An older version of the Yukon Application Suite is"
					    +"\ncurrently installed. This setup will update the"
					    +"\npreviously installed features to version " + IFX_PRODUCT_VERSION 
					    +".\nClick Next to continue.");
	Enable(BACKBUTTON);
	
	RemoveCalcHist();
	Dialog_Services:
	if (AreYukonServicesRunning()) then
		MessageBox(SERVICE_WARNING, WARNING);	
	endif;
	
	Dialog_Backup:
	bOptYes = TRUE;
	nResult = AskOptions(EXCLUSIVE,
			   "Would you like to make a backup of the Yukon directory first?" 
			   +"\n(You will be able to choose log file backup options before starting the backup process.)",
			   "Yes", bOptYes,
			   "No", bOptNo);
	if(nResult = BACK) then
		goto Dialog_Welcome;
	endif;
	if(bOptYes) then
		nResult = BackupTargetDir();
		if(nResult = BACK) then
			goto Dialog_Backup;
		endif;
	endif;
	
	Dialog_ReadyToCopy:
    nResult = SdStartCopy2("Ready to Update Yukon", "Click Install to begin.");
	if(nResult = BACK) then
		goto Dialog_Backup;
	endif;
	
	//Try to stop all the services, just in case one's still running
	StopYukonServices();
	
    if(IsInstalledVersionLesser("7.1.2")) then
        ModifyTomcatConfigSetting();
    endif;
	//replace the pre-5.0 DB cparms if they exist in master.cfg
	sMasterConfig = TARGETDIR + "\\Server\\Config\\master.cfg";
	ReplaceStringInFile(sMasterConfig, "DB_RWDBDLL", "DB_TYPE");
	ReplaceStringInFile(sMasterConfig, "msq15d.dll", "mssql");
	ReplaceStringInFile(sMasterConfig, "ora15d.dll", "oracle");
	
	//Save the configuration of all installed services.
	//This will be read in on the other side of the file transfer
	//to re-populate the settings lost in the upgrade
	SaveServiceSettings();
	
    //YUK-15645 Remove JVM setting while upgrading to 6.5.6 version (or new version ) from lower version.
    RemoveJvmParams();
    
	CopyFile(TARGETDIR + "\\Server\\web\\conf\\server.xml", TARGETDIR + "\\Server\\Config\\Backup\\server_" + CleanupVersionNum(IFX_INSTALLED_VERSION) + ".xml");
	CopyFile(TARGETDIR + "\\Server\\web\\conf\\web.xml", TARGETDIR + "\\Server\\Config\\Backup\\web_" + CleanupVersionNum(IFX_INSTALLED_VERSION) + ".xml");
    // Take backup of logging config files if installedVersion < "7.1.0"
    if(IsInstalledVersionLesser("7.1.0")) then 
        BackupLoggingConfigFiles(sOldVersion);
    endif;
	//remove the YukonWebApplicationService, if it exists (i.e. upgrade install)
	//so the new service installs correctly
	sCMD = SYS32 ^ "sc.exe";
	sArgs = "delete \"YukonWebApplicationService\"";
	LaunchApplication(sCMD, sArgs, "", SW_HIDE, 0, LAAW_OPTION_HIDDEN | LAAW_OPTION_WAIT);
	//get out of "hidden" app-launch mode
	LaunchApplication("", "", "", SW_NORMAL, 0, LAAW_OPTION_WAIT);
	
	//Delete the .jar files in 
	//  Yukon\Server\web\webapps\ROOT\WEB-INF\lib
	//  Yukon\Client\bin
	//(this is to protect from any conflicts that might ensue with upgraded jars,
	//since installscript projects only update files with the same name as a file being
	//installed)
	sDir = TARGETDIR ^ "\\Server\\web\\webapps\\ROOT\\WEB-INF\\lib";
	DeleteFilesOfType(sDir, "jar");
	
	sDir = TARGETDIR ^ "\\Client\\bin";
	DeleteFilesOfType(sDir, "jar");
	
	//Also delete old JRE installer. We only want the latest in this directory 
	sDir = TARGETDIR ^ "\\Server\\Static\\JRE";
	DeleteFilesOfType(sDir, "exe");
    
    //Delete Alarm Monitor icon in case of upgrade when installed version < "7.1.0".
    if(IsInstalledVersionLesser("7.1.0")) then
        DeleteFile ( TARGETDIR ^ "\\Client\\bin\\AlarmMonitor.ico");
        DeleteFile ( TARGETDIR ^ "\\Server\\web\\webapps\\ROOT\\WebConfig\\yukon\\Icons\\32\\AlarmMonitor32.png");
        shortcutDir = FOLDER_PROGRAMS ^ "\\Yukon Clients\\Alarm Monitor.lnk";
        DeleteFile (shortcutDir);
    endif;

    //Delete Static folder in case of upgrade when installed version < "7.2.0".
    if(IsInstalledVersionLesser("7.2.0")) then
        sDir = TARGETDIR ^ "\\Server\\Static";
        DeleteDir(sDir, ALLCONTENTS);
    endif;

    //Delete Runtime folder in case of upgrade when installed version < "7.2.0".
    if(IsInstalledVersionLesser("7.2.0")) then
        sDir = TARGETDIR ^ "\\Runtime";
        DeleteDir(sDir, ALLCONTENTS);
    endif;

	//set installer to update all features that are currently installed
	FeatureReinstall();
	
	//prepare for file transfer
	SetStatusWindow(0, "");
    Enable(STATUSEX);
    StatusUpdate(ON, 100);    
	return 0;
end;

//-----------------------------------------------------------------------------
// The OnUpdateUIAfter event handler function is called by the OnShowUI 
// event handler to display the post-file-transfer user interface for an 
// update setup.
//-----------------------------------------------------------------------------
function OnUpdateUIAfter()
number nResult, nNewVersion, nVMajor, nVMinor, nVBuild;
string sCMD, sProg, sVTemp, sDateString, formattedDateString;
BOOL bOpt, bOptYes, bOptNo;
begin
	//get the DWORD representing the new version number
	VerProductStrToNum (nNewVersion, IFX_PRODUCT_VERSION);
	//get the version parts: major.minor.build
	VerProductVerPartsFromVer (nNewVersion, nVMajor, nVMinor, nVBuild ); 
	//Update the registry info to the newly installed version.
	//This affects updating working correctly, as well as the
	//version # displayed in add/remove programs
	RegDBSetItem(REGDB_UNINSTALL_DISPLAY_VERSION, IFX_PRODUCT_VERSION);
	NumToStr(sVTemp, nVMajor);
	RegDBSetItem(REGDB_UNINSTALL_MAJOR_VERSION, sVTemp);
	NumToStr(sVTemp, nVMinor);
	RegDBSetItem(REGDB_UNINSTALL_MINOR_VERSION, sVTemp);
	NumToStr(sVTemp, nNewVersion);
	RegDBSetItem(REGDB_UNINSTALL_VERSION, sVTemp); 
	//reapply old settings to newly updated services
	LoadServiceSettings();
	
	//adding MessageBroker service if it doesnot exist (while upgrading from 6.5.* to 6.6.*)
	if (ServiceExistsService(SM_NAME) && !ServiceExistsService(BROKER_NAME)) then
    InstallMessageBroker();
    endif;
    
    // Special maintenance for upgrades for 7.1
    RemoveOldDesktopDebugServers();
    if (IsInstalledVersionLesser("7.1.0")) then
    InstallStartStopServicesShortcuts();
    endif;
    // Delete Yukon Client if exist.
    removeOldYukonClientDirectory();
	// Adding watchdog service in case of upgrade from version < 7.1.0
    if (!ServiceExistsService(WATCHDOG_NAME)) then
    InstallWatchdogService();
    endif;
    
	//Remove Lucene indexes so they're rebuilt on startup
	if(ExistsDir(TARGETDIR ^ "cache") = EXISTS) then
		nResult = DeleteDir(TARGETDIR ^ "cache", ALLCONTENTS);
		if(nResult < 0) then
		   MessageBox("Unable to clear Lucene indexes. Please manually delete the \\Yukon\\cache\\ folder to fully clear previous indexes prior to starting the Yukon Web Application service.", WARNING);
		endif;
	endif;
	
    nResult = RemoveActiveMQFolders();
    if(nResult < 0) then
       MessageBox("Unable to clear ActiveMQ cache. Please manually delete the \\Yukon\\Server\\web\\bin\\activemq-data\\ folder and the \\Yukon\\Client\\bin\\activemq-data\\ folder to fully clear previous cache prior to starting services.", WARNING);
    endif;
    
	Dialog_DBToolsFrame:
	Disable(BACKBUTTON);
	Disable(CANCELBUTTON); //You just installed.  There's no going back.
	bOptYes = TRUE;
	AskOptions(EXCLUSIVE,
				"The Yukon files have been updated.  "
				+ "This Yukon software requires a Yukon database"
    			+ " of version " + IFX_PRODUCT_VERSION + ".  " 
    			+ "Would you like to open the DB Tools Frame to "
    			+ "update an existing database?",
    			"Yes", bOptYes,
    			"No", bOptNo);
    if(bOptYes) then
    	//make sure CMD window is not hidden, so we can see logs
   		LaunchApplication("", "", "", SW_NORMAL, 0, LAAW_OPTION_WAIT);
   		//launch DBTF
    	sProg = TARGETDIR ^ "\\Client\\bin\\DBToolsFrame.bat";
		LaunchAppAndWait(sProg, "", LAAW_OPTION_WAIT);
    endif;
    Enable(BACKBUTTON);
	CleanupTomcatWebapps();
    if(IsInstalledVersionLesser("7.1.0")) then 
        GetSystemInfo(DATE, nResult, sDateString);
        // Format date pattern in YYYYMMDD
        formattedDateString = FormatDatePattern(sDateString);
        RenameInstalledDateJavaServicesAndClientLog(formattedDateString);
    endif;
	Dialog_StartServices:
	
	//only give the option to start services if services were installed
	//and only if a restart is not necessary
	if (FeatureIsItemSelected(MEDIA, "Yukon Server") && (!BATCH_INSTALL)) then
		bOptYes = TRUE;
		nResult = AskOptions(EXCLUSIVE,
					"Would you like to start the Yukon Services?",
					"Yes", bOptYes,
					"No", bOptNo);
		if(nResult = BACK) then
			goto Dialog_DBToolsFrame;
		endif;
		if(bOptYes) then
			StartYukonServices(TRUE);
		endif;
	endif;			
	
	//if files were locked, a reboot is required
	if (BATCH_INSTALL) then
    	SdFinishReboot("Yukon Application Suite", 
    				   "The Yukon Application Suite has been updated successfully."
    				   +"\nBefore you can use the program, you must restart your"
    				   +"\ncomputer.", 
    				   SYS_BOOTMACHINE, "", 0);
    else    
		SdFinish("Yukon Application Suite", 
				 "The Yukon Application Suite has been updated successfully.",
				 "Click Finish to exit setup.",
				 "", "", bOpt, bOpt);
	endif;  
end;

//---------------------------------------------------------------------------
// OnCanceling
//
// The OnCanceling event is sent when the installation is cancelled, 
// usually as result of end user clicking Cancel button of a dialog or
// pressing Esc key. Calling Do(EXIT) will also trigger this event.
//---------------------------------------------------------------------------
function OnCanceling()
    STRING szTitle, szMsg1, szMsg2, szOpt1, szOpt2;
    NUMBER bvOpt1, bvOpt2;
begin
    if (IDYES = SprintfBox(MB_YESNO|MB_ICONEXCLAMATION|MB_DEFBUTTON2, SdLoadString(SD_STR_ONCANCELING_TITLE), SdLoadString(SD_STR_ONCANCELING_CONFIRM_MSG))) then
        //Close the current dialog.
        EndCurrentDialog();
        //Display Finish dialog.
        szTitle = "Yukon Installation Canceled";    
        szMsg1 = SdLoadString( SD_STR_ONCANCELING_FINISH_MSG1 );
        szMsg2 = SdLoadString( SD_STR_ONCANCELING_FINISH_MSG2 );    
        szOpt1 = "";
        szOpt2 = "";
        bvOpt1   = FALSE;
        bvOpt2   = FALSE;
        SdFinish ( szTitle, szMsg1, szMsg2 , szOpt1, szOpt2, bvOpt1, bvOpt2 );                       
        
        abort;
    endif;
end;

//---------------------------------------------------------------------------
// OnSetTARGETDIR is called directly by the framework to initialize
// TARGETDIR to it's default value.
// Note: This event is called for all setups.
//---------------------------------------------------------------------------
function OnSetTARGETDIR()
number nIgnore, nResult;
string sTARGETDIR;
begin
    // In maintenance mode the value of TARGETDIR is read from the log file.
    if (MAINTENANCE) then
        return ISERR_SUCCESS;
    endif;
    // Set TARGETDIR to script default.
    TARGETDIR = "<FOLDER_APPLICATIONS>\\<IFX_COMPANY_NAME>\\<IFX_PRODUCT_NAME>";
    // Read TARGETDIR from the media.
    nResult = MediaGetData( MEDIA, MEDIA_FIELD_TARGETDIR, nIgnore, sTARGETDIR );
    // Use the TARGETDIR from the media if anything was read.
    if (nResult >= ISERR_SUCCESS && StrLengthChars(sTARGETDIR)) then
        TARGETDIR = sTARGETDIR;
    endif;
end;

//---------------------------------------------------------------------------
// OnUninstall
//
// The OnUninstall event is called when the setup is run with the -uninst
// switch, in this case this is the only event that will be called.
//---------------------------------------------------------------------------
function OnUninstall()
string szIgnore, szMsg;
string sDir;
number nResult;
BOOL bOpt1, bOpt2, bOptNo, bOptYes;
begin
    //set the string shown on the installer windows' title bars
	IFX_SETUP_TITLE = "Yukon Application Suite - Installation Wizard";
    
    // Hide the initial progress dialog.
    Disable( DIALOGCACHE );

    // Added in InstallShield 15 - Show an appropriate error message if
    // -uninst is specified and the product is not installed.
    if( !MAINTENANCE ) then
		szMsg = SdLoadString( IDS_IFX_ERROR_PRODUCT_NOT_INSTALLED_UNINST );
   		SdSubstituteProductInfo( szMsg );
		MessageBox( szMsg, SEVERE );
		abort;
    endif;

    Dialog_Welcome:
	Disable(BACKBUTTON);
	nResult = SdWelcome("Yukon Application Suite", 
					"This program will completely uninstall the Yukon Application"
					+ "\nSuite.  Click Next to continue.  Click Cancel to abort.");
    
    Dialog_Services:
	if (AreYukonServicesRunning()) then
        StopYukonServices();
		MessageBox(SERVICE_WARNING, WARNING);	
	endif;
    
    Dialog_Backup:
    bOptNo = TRUE;
	AskOptions(EXCLUSIVE,
			   "Would you like to make a backup of the Yukon directory first?" 
			   +"\n(You will be able to choose log file backup options before starting the backup process.)",
			   "Yes", bOptYes,
			   "No", bOptNo);
	if(bOptYes) then
		nResult = BackupTargetDir();
		if(nResult = BACK) then
			goto Dialog_Welcome;
		endif;
	endif;
	Enable(BACKBUTTON);
    
    // Remove all features.
	FeatureRemoveAllInMediaAndLog();
	//Remove Lucene indexes so they're rebuilt on startup
    sDir = TARGETDIR ^ "\\cache";
    DeleteDir(sDir, ALLCONTENTS);

    // Remove temp folder gracefully
    sDir = TARGETDIR ^ "\\Client\\bin\\temp";
    DeleteDir(sDir, ALLCONTENTS);
    
    // Delete Runtime folder.
        sDir = TARGETDIR ^ "\\Runtime";
        DeleteDir(sDir, ALLCONTENTS);

    // Delete Yukon Client if exist.
    removeOldYukonClientDirectory();
    RemoveActiveMQFolders();

    // Added in 11.0, disable the cancel button during uninstall.
    if( REMOVEALLMODE ) then
        Disable( CANCELBUTTON );
    endif;
	// Added in 11.0 - Set appropriate StatusEx static text.
	// Moved in 12.1 - Set before calling Enable( STATUSEX ) to ensure proper product name updating.
    SetStatusExStaticText( SdLoadString( IDS_IFX_STATUSEX_STATICTEXT_MAINTUI_REMOVEALL ) );
    // Show Status
	SetStatusWindow( 0, "" );
	Enable( STATUSEX );
	StatusUpdate( ON, 100 );
    // Uninstall
	nResult = FeatureTransferData( MEDIA );
    // Handle move data error and abort if error occurred.
	if( nResult < ISERR_SUCCESS ) then
		OnComponentError();
		abort;
	endif;
    // Disable Status
	Disable( STATUSEX );
    // Show final dialog.
    if ( BATCH_INSTALL ) then
        SdFinishReboot( SdLoadString(IFX_SDFINISH_REMOVE_TITLE), SdLoadString(IFX_SDFINISH_REMOVE_MSG2), SYS_BOOTMACHINE, "", 0 );
    else    
        SdFinish( SdLoadString(IFX_SDFINISH_REMOVE_TITLE), SdLoadString(IFX_SDFINISH_REMOVE_MSG1), "", "" , "", bOpt1 , bOpt2 );
    endif;
end;

//---------------------------------------------------------------------------
// OnMaintUIBefore
//
// Maintenance UI Sequence - Before Move Data
//
// The OnMaintUIBefore event is called by OnShowUI when the setup is
// running in maintenance mode. By default this event displays UI that
// allows the end user to add or remove features, repair currently
// installed features or uninstall the application.
//
// Note: This event will not be called automatically in a
// program...endprogram style setup.
//---------------------------------------------------------------------------
function OnMaintUIBefore()
    number nResult, nType;
    string szTitle, szMsg, sMsg1, sMsg2, sInstalledVersion, sDir;
    BOOL bOpt, bOptNo, bOptYes;
begin
	//set the string shown on the installer windows' title bars
	IFX_SETUP_TITLE = "Yukon Application Suite - Installation Wizard";
	//nType defaults to MODIFY.
	nType = MODIFY;
    //Initialize SQL
    OnSQLServerInitializeMaint();

//check that we're not trying to 'downgrade'
if(IsDowngrade()) then
		sInstalledVersion = CleanupInstalledVersionNum();
		sMsg1 = "Version " + sInstalledVersion + " of the Yukon Application "
				+ "Suite is currently installed. This installer is version "
				+ IFX_PRODUCT_VERSION + ".";
		sMsg2 = "The Yukon Installer cannot revert the product to a previous "
				+ "version.";
		SdFinish("Newer Version Installed", sMsg1, sMsg2, "", "", bOpt, bOpt);
		abort; //shut down the installer
endif;

CopyFile(TARGETDIR + "\\Server\\web\\conf\\server.xml", TARGETDIR + "\\Server\\Config\\Backup\\server_" + CleanupVersionNum(IFX_INSTALLED_VERSION) + ".xml");
CopyFile(TARGETDIR + "\\Server\\web\\conf\\web.xml", TARGETDIR + "\\Server\\Config\\Backup\\web_" + CleanupVersionNum(IFX_INSTALLED_VERSION) + ".xml");
CleanupTomcatWebapps();
//Beginning of UI Sequence
Dlg_Start:
    if( !REMOVEONLY ) then
    	// In standard mode show maintenance dialog
	    Disable( BACKBUTTON );
	    nType = SdWelcomeMaint( szTitle, szMsg, nType );
	    Enable( BACKBUTTON );
        nResult = NEXT;
    else
        // Hide the initial progress dialog as otherwise the user can
        // click on it, and hide the MessageBox.
        Disable( DIALOGCACHE );
        // In RemoveOnly mode, set to remove.
        nType = REMOVEALL;
    endif;

Dlg_SdFeatureTree:
	if ( nType = MODIFY ) then
		szTitle = "";
		szMsg = SdLoadString( SD_STR_COMPONENT_MAINT_MSG );
		nResult = SdFeatureTree( szTitle, szMsg, TARGETDIR, "", -1 );
		if ( nResult = BACK ) goto Dlg_Start;
    endif;

Dlg_ObjDialogs:
    nResult = ShowObjWizardPages( nResult );
    if ( ( nResult = BACK ) && ( nType != MODIFY ) ) goto Dlg_Start;
    if ( ( nResult = BACK ) && ( nType = MODIFY ) ) goto Dlg_SdFeatureTree;
	switch(nType)
        case REMOVEALL:
			Dialog_UninstWelcome:
			Disable(BACKBUTTON);
			nResult = SdWelcome("Uninstall Yukon?", 
							"This program will completely uninstall the Yukon Application "
							+ "Suite.  Click Next to continue.  Click Cancel to abort.");
			Enable(BACKBUTTON);
			Dialog_UninstServices:
			if (AreYukonServicesRunning()) then
				StopYukonServices();
				MessageBox(SERVICE_WARNING, WARNING);	
			endif;
			Dialog_UninstBackup:
			bOptYes = TRUE;
			nResult = AskOptions(EXCLUSIVE,
					   "Would you like to make a backup of the Yukon directory first?" 
						+"\n(You will be able to choose log file backup options before starting the backup process.)",
					   "Yes", bOptYes,
					   "No", bOptNo);
			if(nResult = BACK) then
				goto Dialog_UninstWelcome;
			endif;
			if(bOptYes) then
				nResult = BackupTargetDir();
				if(nResult = BACK) then
					goto Dialog_UninstWelcome;
				endif;
			endif;
			Dialog_UninstReady:
			nResult = SdWelcome("Ready to Uninstall Yukon", 
								"The Yukon Application Suite is ready to be uninstalled."
								+ "\nClick Next to begin uninstallation or Cancel to abort.");
			if(nResult = BACK) then
				goto Dialog_UninstBackup;
			endif;
            //Remove Lucene indexes so they're rebuilt on startup
            sDir = TARGETDIR ^ "\\cache";
            DeleteDir(sDir, ALLCONTENTS);
            
            RemoveActiveMQFolders();
			
			// Ensure that all previously installed features are removed.
			FeatureRemoveAllInMediaAndLog();
            // Added in 11.0 - Set appropriate StatusEx static text.
            SetStatusExStaticText( SdLoadString( IDS_IFX_STATUSEX_STATICTEXT_MAINTUI_REMOVEALL ) );
        case REPAIR:	
			if (AreYukonServicesRunning()) then
				StopYukonServices();
				MessageBox(SERVICE_WARNING, WARNING);	
			endif;
			
			//Save the configuration of all installed services.
			//This will be read in on the other side of the file transfer
			//to re-populate the settings lost in the repair
			SaveServiceSettings();
			
			// Changed for DevStudio 9, Disk1 files are now always updated when installed
			// so when running from ADDREMOVE we need to prevent these files from being
			// updated since this will result in files being updated that are locked by the setup.
			// Updating these files when running from ADDREMOVE should not be needed since updates
			// are not run directly from Add/Remove.
            if( ADDREMOVE ) then
                // Reinstall all previously installed features, except
                // disk1 features.
                FeatureUpdate( "" );
            else
                // Reinstall all previously installed features.
                FeatureReinstall();
            endif;
            
            // Added in 11.0 - Set appropriate StatusEx static text.
            SetStatusExStaticText( SdLoadString( IDS_IFX_STATUSEX_STATICTEXT_MAINTUI_REPAIR ) );
        case MODIFY:
            if (AreYukonServicesRunning()) then
				StopYukonServices();
				MessageBox(SERVICE_WARNING, WARNING);	
			endif;
            SaveServiceSettings();
            // Added in 11.0 - Set appropriate StatusEx static text.
            SetStatusExStaticText( SdLoadString( IDS_IFX_STATUSEX_STATICTEXT_MAINTUI_MODIFY ) );
    endswitch;
end;

//---------------------------------------------------------------------------
// OnMaintUIAfter
//
// The OnMaintUIAfter event called by OnShowUI after the file transfer
// of the setup when the setup is running in maintenance mode. By default
// this event displays UI that informs the end user that the maintenance setup
// has been completed successfully.
//
// Note: This event will not be called automatically in a
// program...endprogram style setup.
//---------------------------------------------------------------------------
function OnMaintUIAfter()
    STRING szTitle, szMsg1, szMsg2, szOpt1, szOpt2;
    NUMBER bvOpt1, bvOpt2, bOptYes, bOptNo;
begin
    Disable(BACKBUTTON);
 	
 	//reapply old settings to newly updated services
    if( !REMOVEALLMODE ) then
        LoadServiceSettings();
    endif;
 	
 	if (FeatureIsItemSelected(MEDIA, "Yukon Server") && (!BATCH_INSTALL)) then
 		bOptYes = TRUE;
 		AskOptions(EXCLUSIVE,
 			"Would you like to start the Yukon Services?",
 			"Yes", bOptYes,
 			"No", bOptNo);
 		if(bOptYes) then
 			StartYukonServices(TRUE);
 		endif;
	endif;
    
    ShowObjWizardPages(NEXT);
    // Added - Version 9.5 - Use appropriate strings for complete
    // uninstall.
    if( REMOVEALLMODE ) then
        szTitle = "Uninstall Complete";
        szMsg1 = "The Yukon Application Suite has been successfully uninstalled.";
    else
        szTitle = SdLoadString(IFX_SDFINISH_MAINT_TITLE);    
        szMsg1  = SdLoadString(IFX_SDFINISH_MAINT_MSG1);
    endif;

	szMsg2 = "";    
    szOpt1 = "";
    szOpt2 = "";
	bvOpt1   = FALSE;
    bvOpt2   = FALSE;    

    if ( BATCH_INSTALL ) then
    	SdFinishReboot ( szTitle , szMsg1 , SYS_BOOTMACHINE , szMsg2 , 0 );
    else    
       	SdFinish ( szTitle , szMsg1 , szMsg2 , szOpt1 , szOpt2 , bvOpt1 , bvOpt2 );
    endif;
end;


